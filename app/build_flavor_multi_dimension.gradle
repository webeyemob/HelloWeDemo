apply plugin: 'com.android.application'
// ModooPlay
apply plugin: 'com.tgcenter.unified'

// flavor 多 dimension
// 1、建议仅在一个 dimension 中 配置
// 2、如果在多个 dimendion 中配置，则按照 flavorDimensions 中的次序来查找配置
// 3、flavor 可单独配置 config 和 dependencies
// 4、flavor 中不配置或配置 null 都表示不接入

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.tgcenter.demo"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
    }

    // 下面的示例中，在两个 dimension 中都有配置，则优先使用 "channel" 中的配置
    // 只有当 "channel" 中无配置时，才使用 "price" 中的配置
    flavorDimensions "channel", "price"


    // 在 productFlavors 中：
    // "channel": toutiao_inner(有配置)，nullTGCenter(有配置 null)，noTGCenter(无配置)
    // "price":   paid(有配置)，         free(无配置)

    // 则配置结果如下：
    // toutiao_innerPaid：使用 toutiao_inner 中的配置
    // toutiao_innerFree：使用 toutiao_inner 中的配置

    // nullTGCenterPaid：使用 nullTGCenter 中的配置（null 表示不接入）
    // nullTGCenterFree：使用 nullTGCenter 中的配置（null 表示不接入）

    // noTGCenterPaid：使用 paid 中的配置
    // noTGCenterFree：无配置（不接入）
    productFlavors {
        toutiao_inner {
            dimension "channel"
            tgCenter {
                config {
                    appId "appId_toutiao_inner"
                }
                dependencies {
                    tgCenter true
                    privacy true
                    embed true
                    umeng true
                    appsFlyer true
                    rangersAppLog_Lite_cn true
                    taurusXAds true
                    taurusXAdsNetwork = [
                            toutiao: true
                    ]
                }
            }
        }

        nullTGCenter {
            dimension "channel"
            tgCenter {
                config null
                dependencies null
            }
        }

        noTGCenter {
            dimension "channel"
        }

        paid {
            dimension "price"
            tgCenter {
                config {
                    appId "appId_paid"
                }
                dependencies {
                    tgCenter true
                    privacy true
                    embed true
                    umeng true
                    appsFlyer true
                    taurusXAds true
                    taurusXAdsNetwork = [
                            kuaishou: true
                    ]
                }
            }
        }

        free {
            dimension "price"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
}

configurations.all {
    resolutionStrategy {
    }
}