apply plugin: 'com.android.application'
// ModooPlay
apply plugin: 'com.tgcenter.unified'

// flavor 多 dimension【有全局配置】
// 1、建议仅在一个 dimension 中配置
// 2、如果在多个 dimendion 中配置，则按照 flavorDimensions 中的次序来查找配置
// 3、flavor 都不配置则使用全局配置
// 4、flavor 可单独配置 config 和 dependencies
// 5、flavor 中配置 null 覆盖全局配置，表示不接入

// 全局配置
tgCenter {
    config {
        appId "4b4b6832-4267-42db-9c04-d517d5288bbb"
    }
    dependencies {
        tgCenter true
        privacy true
        embed true
        umeng true
        appsFlyer true
        rangersAppLog_Lite_cn true
        taurusXAds true
        taurusXAdsNetwork = [
                ylh: true
        ]
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.modooplay.demo"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 下面的示例中，在两个 dimension 中都有配置，则优先使用 "channel" 中的配置
    // 只有当 "channel" 中无配置时，才使用 "price" 中的配置
    // 如果都无配置，则使用全局配置
    flavorDimensions "channel", "price"


    // 在 productFlavors 中：
    // "channel": toutiao_inner(有配置)，nullTGCenter(有配置 null)，noTGCenter(无配置)
    // "price":   paid(有配置)，         free(无配置)

    // 则配置结果如下：
    // toutiao_innerPaid：使用 toutiao_inner 中的 config，使用 paid 中的 dependencies
    // toutiao_innerFree：使用 toutiao_inner 中的 config，使用全局 dependencies

    // nullTGCenterPaid：使用 nullTGCenter 中的配置（null 表示不接入）
    // nullTGCenterFree：使用 nullTGCenter 中的配置（null 表示不接入）

    // noTGCenterPaid：使用全局 config，使用 paid 中的 dependencies
    // noTGCenterFree：无配置（使用全屏配置）
    productFlavors {
        toutiao_inner {
            dimension "channel"
            tgCenter {
                config {
                    appId "appId_toutiao_inner"
                }
                // 不配置 dependencies，使用 "price" 中的配置，
                // 如果 "price" 无配置，则使用全局配置中的 dependencies
            }
        }

        nullTGCenter {
            dimension "channel"
            tgCenter {
                config null
                dependencies null
            }
        }

        noTGCenter {
            dimension "channel"
        }

        paid {
            dimension "price"
            tgCenter {
                // 不配置 config，使用 "channel" 中的配置，
                // 如果 "channel" 中无配置，则使用全局配置中的 config
                dependencies {
                    tgCenter true
                    privacy true
                    embed true
                    umeng true
                    appsFlyer true
                    taurusXAds true
                    taurusXAdsNetwork = [
                            kuaishou: true
                    ]
                }
            }
        }

        free {
            dimension "price"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
}

configurations.all {
    resolutionStrategy {
    }
}